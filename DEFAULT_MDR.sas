/*******************************************************************
	NAME:	DEFAULT
	DESC:	Denne kode finder defaulted kunder og deres tilhørende virksomhed,
            hvis dette eksistere baseret på ultimo måned basis

			Det er blevet besluttet at et default_flag = 1 bliver overført til del_modparten og
			og dens tilhørende kundenr, for I/S, og EMV K/S. Dette er dog ikke gældende 
			for HOLDING, APS og A/S.    
*******************************************************************/
/* cas mySession terminate;  */
cas mySession sessopts=(timeout=3500000);
caslib _all_ assign;
options casdatalimit=500M;

%LET START_PERIODAG = 2022-01-01; 
%LET SLUT_PERIODAG = 2023-03-31; 

%LET SQL_START_PERIODAG=%STR(%')&START_PERIODAG.%STR(%');
%LET SQL_SLUT_PERIODAG=%STR(%')&SLUT_PERIODAG.%STR(%');


PROC FEDSQL SESSREF=mySession;
drop table CASUSER.RELATIONER_FULD FORCE;
CREATE TABLE CASUSER.RELATIONER_FULD AS SELECT 
DAG.DATO
,REL.*
FROM (SELECT DATO FROM RAT_DATA.DAGE 

WHERE DATO < &SQL_SLUT_PERIODAG. and &SQL_START_PERIODAG. <DATO AND BKDAG_MND_ULTIMO = 1
)
AS DAG

INNER JOIN (
SELECT 
KUNDENR
,NAVN
,CVRNR
,VIRKSOMHEDSNAVN
,SLSKFORM
,DB07
,EJERFORHOLD
,EJERFORHOLD_TXT
,EJERANDELPCT
,ENGAGEMENT_KREDIT_VIRKSOMHED
,ANTAL_EJERE
,START_DATO
,SLUT_DATO


FROM RAT_DATA.RELATIONER 

WHERE START_DATO < &SQL_SLUT_PERIODAG. AND &SQL_START_PERIODAG. <SLUT_DATO 
	  AND (ENGAGEMENT_KREDIT_VIRKSOMHED=. OR ENGAGEMENT_KREDIT_VIRKSOMHED<=250000 )
)
AS REL
/* ON DAG.DATO between REL.START_DATO and REL.SLUT_DATO-1 */
ON REL.START_DATO<= DAG.DATO AND DAG.DATO<REL.SLUT_DATO
;
QUIT; 



PROC FEDSQL SESSREF=mySession;
drop table CASUSER.DEFAULT_DAGLIG FORCE;
CREATE TABLE CASUSER.DEFAULT_DAGLIG AS SELECT 
DAG.DATO
,DEF.KUNDENR
,DEF.MODPARTS_ID
,DEF.DEL_MODPARTS_ID
,DEF.DEFAULT_FLAG
,DEF.PERIODE_START
,DEF.PERIODE_SLUT
,DEF.START_DATO
,DEF.SLUT_DATO

FROM (SELECT DATO FROM RAT_DATA.DAGE 

WHERE DATO < &SQL_SLUT_PERIODAG. and &SQL_START_PERIODAG. <DATO AND BKDAG_MND_ULTIMO = 1
)
AS DAG

INNER JOIN (
SELECT 
KUNDENR
,MODPARTS_ID
,DEL_MODPARTS_ID
,DEFAULT_FLAG
,PERIODE_START
,PERIODE_SLUT
,START_DATO
,SLUT_DATO

FROM RAT_DATA.DEFAULT_DATA

WHERE START_DATO < &SQL_SLUT_PERIODAG. and &SQL_START_PERIODAG. <SLUT_DATO 
)
AS DEF

ON DEF.START_DATO <= DAG.DATO AND DAG.DATO < DEF.SLUT_DATO
;
QUIT; 

/* TJEKKER OM VIRKSOMHEDEN HAR DEFAULT_FLAG = 1 OG DANNER NY 0/1 VARIABLE DEFAULT_VIRK */
PROC FEDSQL SESSREF=mySession;
drop table CASUSER.DEFAULT_VIRKSOMHED FORCE;
CREATE TABLE CASUSER.DEFAULT_VIRKSOMHED AS SELECT 
REL.KUNDENR
,DEF.DEL_MODPARTS_ID AS DEL_MODPARTS_ID_VIRK
,DEF.MODPARTS_ID AS MODPARTS_ID_VIRK
,REL.DATO
,REL.CVRNR 
,REL.EJERFORHOLD
,REL.EJERFORHOLD_TXT
,REL.ANTAL_EJERE
,DEF.DEFAULT_FLAG AS DEFAULT_VIRK

FROM CASUSER.RELATIONER_FULD AS REL

LEFT JOIN DEFAULT_DAGLIG AS DEF
ON REL.CVRNR=DEF.KUNDENR AND REL.DATO=DEF.DATO

HAVING DEFAULT_FLAG = 1 
;
QUIT; 


/* SAMMENSÆTTER ORIGNAL DEFAULT DATASÆT MED DEFAULT FOR VIRKSOMHEDER */
DATA CASUSER.DEFAULT_TOTAL  ( REPLACE=yes); 
SET CASUSER.DEFAULT_DAGLIG CASUSER.DEFAULT_VIRKSOMHED; 
RUN; 


/* FINDER DEL_MODPARTS_ID FOR HVERT KUNDENR, FOR AT FINDE EJER/PARNTER FORHOLD */
PROC FEDSQL SESSREF=mySession;
drop table CASUSER.DEFAULT_DELMOD FORCE;
CREATE TABLE CASUSER.DEFAULT_DELMOD AS SELECT 
DEF.KUNDENR AS KUNDENR_ORG 
,STAM.DEL_MODPARTS_ID
,STAM.MODPARTS_ID
,DEF.DEL_MODPARTS_ID_VIRK
,DEF.MODPARTS_ID_VIRK
,DEF.DATO
,DEF.CVRNR
,DEF.EJERFORHOLD
,DEF.EJERFORHOLD_TXT
,DEF.ANTAL_EJERE
,DEF.SLUT_DATO 
,DEF.START_DATO 
,DEF.DEFAULT_FLAG
,DEF.DEFAULT_VIRK

FROM (
SELECT 
KUNDENR
,DEL_MODPARTS_ID
,MODPARTS_ID
,START_DATO
,SLUT_DATO 

FROM RAT_DATA.STAMOPLYSNINGER 
WHERE START_DATO < &SQL_SLUT_PERIODAG. AND &SQL_START_PERIODAG. <SLUT_DATO 
)
AS STAM

LEFT JOIN CASUSER.DEFAULT_TOTAL AS DEF
ON DEF.KUNDENR = STAM.KUNDENR AND STAM.START_DATO <= DEF.DATO AND DEF.DATO < STAM.SLUT_DATO
;
QUIT;

PROC FEDSQL SESSREF=mySession;
DROP TABLE  CASUSER.DEFAULT_DELMOD_MAX FORCE;
CREATE TABLE CASUSER.DEFAULT_DELMOD_MAX  AS SELECT 
KUNDENR_ORG
,MAX(DEL_MODPARTS_ID) AS DEL_MODPARTS_ID
,MAX(MODPARTS_ID) AS MODPARTS_ID
,MAX(DEL_MODPARTS_ID_VIRK) AS DEL_MODPARTS_ID_VIRK
,MAX(MODPARTS_ID_VIRK) AS MODPARTS_ID_VIRK
,MAX(CVRNR) AS CVRNR
,MAX(EJERFORHOLD) AS EJERFORHOLD
,MAX(DEFAULT_FLAG) AS DEFAULT_FLAG
,MAX(DEFAULT_VIRK) AS DEFAULT_VIRK
,DATO
,MAX(START_DATO) AS START_DATO
,MAX(SLUT_DATO) AS SLUT_DATO
FROM CASUSER.DEFAULT_DELMOD

GROUP BY 
KUNDENR_ORG
,DATO
;
QUIT;


/* FOR HVERT DEL_MODPARTS_ID FINDES DE TILHØRENDE KUNDENR*/
PROC FEDSQL SESSREF=mySession;
drop table CASUSER.DEFAULT_DELMOD_ALLE FORCE;
CREATE TABLE CASUSER.DEFAULT_DELMOD_ALLE AS SELECT 
 STAM.KUNDENR  
,DEL.KUNDENR_ORG  
,DEL.DEL_MODPARTS_ID 
,DEL.MODPARTS_ID  
,DEL.DEL_MODPARTS_ID_VIRK
,DEL.MODPARTS_ID_VIRK
,DEL.CVRNR
,DEL.EJERFORHOLD
,DEL.DEFAULT_FLAG
,DEL.DEFAULT_VIRK
,DEL.DATO
,DEL.START_DATO 
,DEL.SLUT_DATO  
FROM CASUSER.DEFAULT_DELMOD_MAX AS DEL

LEFT JOIN (
SELECT 
KUNDENR
,DEL_MODPARTS_ID
,MODPARTS_ID
,START_DATO
, SLUT_DATO 

FROM RAT_DATA.STAMOPLYSNINGER 
WHERE START_DATO < &SQL_SLUT_PERIODAG. AND &SQL_START_PERIODAG. <SLUT_DATO 
)
AS STAM 
ON DEL.DEL_MODPARTS_ID = STAM.DEL_MODPARTS_ID AND STAM.START_DATO <= DEL.DATO AND DEL.DATO < STAM.SLUT_DATO

;
QUIT;



PROC FEDSQL SESSREF=mySession;
DROP TABLE  CASUSER.DEFAULT_DELMOD_ALLE_MAX FORCE;
CREATE TABLE CASUSER.DEFAULT_DELMOD_ALLE_MAX  AS SELECT 
KUNDENR
,MAX(DEL_MODPARTS_ID) AS DEL_MODPARTS_ID
,MAX(MODPARTS_ID) AS MODPARTS_ID
,MAX(DEL_MODPARTS_ID_VIRK) AS DEL_MODPARTS_ID_VIRK
,MAX(MODPARTS_ID_VIRK) AS MODPARTS_ID_VIRK
,MAX(CVRNR) AS CVRNR
,MAX(EJERFORHOLD) AS EJERFORHOLD
,MAX(DEFAULT_FLAG) AS DEFAULT_FLAG
,MAX(DEFAULT_VIRK) AS DEFAULT_VIRK
,DATO
,MAX(START_DATO) AS START_DATO
,MAX(SLUT_DATO) AS SLUT_DATO
FROM CASUSER.DEFAULT_DELMOD_ALLE

GROUP BY 
KUNDENR
,DATO
;
QUIT;

/* Overskriver default_flag for EMV, I/S og K/S - Det nye flag hedder default_flag_korr*/
PROC FEDSQL SESSREF=mySession;
DROP TABLE  CASUSER.DEFAULT_KORR_TEMP FORCE;
CREATE TABLE CASUSER.DEFAULT_KORR_TEMP  AS SELECT 
KUNDENR
,DEL_MODPARTS_ID
,MODPARTS_ID
,DEL_MODPARTS_ID_VIRK
,MODPARTS_ID_VIRK
,CVRNR
,EJERFORHOLD
,DEFAULT_FLAG
,DEFAULT_VIRK
,DATO
,CASE 
/*EMV, I/S, K/S */
	WHEN (EJERFORHOLD in (31,32,33) AND DEFAULT_VIRK = 1 ) THEN 1 

/*A/S, APS, KA/S, HOLDING */
	WHEN EJERFORHOLD in (34,35,36,999) AND DEFAULT_VIRK = 1 THEN 0 

/* GENERELT*/
	WHEN DEFAULT_FLAG = 1 THEN 1

ELSE 0 END AS DEFAULT_FLAG_KORR  

FROM CASUSER.DEFAULT_DELMOD_ALLE_MAX
;
QUIT; 

DATA CASUSER.DEFAULT_KORR_TEMP ( REPLACE=yes ); 
SET CASUSER.DEFAULT_KORR_TEMP; 

WHERE DEFAULT_FLAG_KORR = 1;

RUN;

PROC FEDSQL SESSREF=mySession;
drop table CASUSER.STAMOPLYSNINGER_DAGLIG FORCE;
CREATE TABLE CASUSER.STAMOPLYSNINGER_DAGLIG AS SELECT 
DAG.DATO
,STAM.KUNDENR
,STAM.MODPARTS_ID
,STAM.DEL_MODPARTS_ID

FROM (SELECT DATO FROM RAT_DATA.DAGE 

WHERE DATO < &SQL_SLUT_PERIODAG. and &SQL_START_PERIODAG. <DATO AND BKDAG_MND_ULTIMO = 1
)
AS DAG

INNER JOIN (
SELECT 
KUNDENR
,MODPARTS_ID
,DEL_MODPARTS_ID
,START_DATO
,SLUT_DATO

FROM RAT_DATA.STAMOPLYSNINGER 

WHERE START_DATO < &SQL_SLUT_PERIODAG. and &SQL_START_PERIODAG. <SLUT_DATO 
)
AS STAM

ON STAM.START_DATO <= DAG.DATO AND DAG.DATO < STAM.SLUT_DATO
;
QUIT; 



PROC FEDSQL SESSREF=mySession;
drop table CASUSER.DEFAULT_KORR_STAM FORCE;
CREATE TABLE CASUSER.DEFAULT_KORR_STAM AS SELECT 
STAM.KUNDENR
,STAM.DEL_MODPARTS_ID
,STAM.MODPARTS_ID
,DEF.DEL_MODPARTS_ID_VIRK
,DEF.MODPARTS_ID_VIRK
,DEF.CVRNR
,DEF.EJERFORHOLD
,DEF.DEFAULT_FLAG
,DEF.DEFAULT_VIRK
,DEF.DEFAULT_FLAG_KORR
,STAM.DATO

FROM CASUSER.STAMOPLYSNINGER_DAGLIG AS STAM
LEFT JOIN CASUSER.DEFAULT_KORR_TEMP AS DEF
ON DEF.KUNDENR = STAM.KUNDENR AND STAM.DATO = DEF.DATO

;
QUIT;



DATA CASUSER.DEFAULT_KORR ( REPLACE=yes DROP=DATO); 
SET CASUSER.DEFAULT_KORR_STAM; 

DEFAULT_FLAG_KORR = SUM(DEFAULT_FLAG_KORR,0); 

/* START_DATO=DATO;  */
/* SLUT_DATO=intnx('day',dato,+1);  */
SLUT_DATO=intnx('month',dato,-1,'E');
SLUT_DATO=intnx('month',dato,0,'E');
	
start_dttm=dhms(start_dato,0,0,0) ;
slut_dttm=dhms(SLUT_DATO,23,59,59)-86400  ;
format START_DATO SLUT_DATO DATE9. slut_dttm start_dttm datetime20. ;

WHERE KUNDENR NE . ;
RUN;





 %LET VIYA_LIBRARY=RAT_DATA;  
 %LET TABEL_NAVN=DEFAULT_KORR;  

 	proc casutil  incaslib="casuser" outcaslib="&VIYA_LIBRARY." ;     
 		save casdata="&TABEL_NAVN." replace;  
 		DROPTABLE CASDATA="&TABEL_NAVN." INCASLIB="&VIYA_LIBRARY." QUIET; 
 		load casdata="&TABEL_NAVN..sashdat"  
 		casout="&TABEL_NAVN." 	 
 		incaslib="&VIYA_LIBRARY."  promote; 
 	run;   

PROC FEDSQL SESSREF=mySession;
DROP TABLE CASUSER.DEFAULT_DAGLIG FORCE;
DROP TABLE CASUSER.DEFAULT_DELMOD FORCE;
DROP TABLE CASUSER.DEFAULT_DELMOD_ALLE FORCE;
DROP TABLE CASUSER.DEFAULT_DELMOD_ALLE_MAX FORCE;
DROP TABLE CASUSER.DEFAULT_DELMOD_MAX FORCE;
DROP TABLE CASUSER.DEFAULT_KORR FORCE;
DROP TABLE CASUSER.DEFAULT_KORR_TEMP FORCE;
DROP TABLE CASUSER.DEFAULT_TOTAL FORCE;
DROP TABLE CASUSER.DEFAULT_VIRKSOMHED FORCE;
DROP TABLE CASUSER.RELATIONER_FULD FORCE;
;
QUIT;

